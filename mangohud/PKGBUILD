# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <https://github.com/blacksky3>
#Credits: Ranieri Althoff <ranisalt+aur at gmail dot com>
#Credits: Mark Wagie <mark dot wagie at tutanota dot com>

pkgbase=mangohud
pkgname=(mangohud mangoapp)
pkgver=0.6.8
pkgrel=1
_mango_commit=efdcc6d2f54e37a3a32475453407f1eb33d1bef2
_imgui_ver=1.89.2
_imgui_meson_ver=1.89.2-1
_spdlog_ver=1.11.0
_spdlog_meson_ver=1.11.0-1
arch=(x86_64)
url='https://github.com/flightlessmango/MangoHud'
license=(MIT)
makedepends=(appstream dbus git glew glfw-x11 glslang libglvnd libxnvctrl meson
             nlohmann-json python-mako vulkan-headers libx11 wayland)
source=(git+https://github.com/flightlessmango/MangoHud.git#commit=${_mango_commit}
        git+https://github.com/flightlessmango/minhook.git
        https://github.com/ocornut/imgui/archive/v${_imgui_ver}/imgui-${_imgui_ver}.tar.gz
        https://wrapdb.mesonbuild.com/v2/imgui_${_imgui_meson_ver}/get_patch#/imgui-${_imgui_meson_ver}-wrap.zip
        spdlog-${_spdlog_ver}.tar.gz::https://github.com/gabime/spdlog/archive/v${_spdlog_ver}.tar.gz
        https://wrapdb.mesonbuild.com/v2/spdlog_${_spdlog_meson_ver}/get_patch#/spdlog-${_spdlog_meson_ver}-wrap.zip)

prepare(){
  cd $srcdir/MangoHud

  git submodule init modules/minhook
  git config submodule.minhook.url $srcdir/minhook
  git -c protocol.file.allow=always submodule update

  ln -sfv \
    "$srcdir/imgui-${_imgui_ver}" \
    "$srcdir/imgui-${_imgui_meson_ver}" \
    "$srcdir/spdlog-${_spdlog_ver}" \
    "$srcdir/spdlog-${_spdlog_meson_ver}" \
    subprojects
}

build(){
  cd $srcdir/MangoHud

  rm -rf -v build_64

  mkdir build_64

  meson setup build_64/ \
  -D buildtype=release \
  -D prefix=/usr \
  -D libdir=lib \
  -Dglibcxx_asserts=false \
  -Duse_system_vulkan=enabled \
  -Duse_system_spdlog=disabled \
  -Dappend_libdir_mangohud=false \
  -Dld_libdir_prefix=false \
  -Dld_libdir_abs=false \
  -Dprepend_libdir_vk=true \
  -Dinclude_doc=true \
  -Dwith_nvml=enabled \
  -Dwith_xnvctrl=enabled \
  -Dwith_x11=enabled \
  -Dwith_wayland=enabled \
  -Dwith_dbus=enabled \
  -Dwith_dlsym=disabled \
  -Dloglevel=off \
  -Dmangoapp=true \
  -Dmangohudctl=true \
  -Dmangoapp_layer=true \
  -Dtests=disabled \

  #meson configure build_64/

  ninja -C build_64/

  meson compile -C build_64/
}

package_mangohud(){
  pkgdesc='A Vulkan overlay layer for monitoring FPS, temperatures, CPU/GPU load and more'
  depends=(dbus fmt gcc-libs libx11 wayland spdlog vulkan-icd-loader)
  optdepends=('libxnvctrl: NVIDIA GPU stats by XNVCtrl'
              'mangoapp')
  replaces=("$pkgname-x11" "$pkgname-wayland")

  meson install --tags runtime,scripts -C $srcdir/MangoHud/build_64 --destdir "$pkgdir"

  meson install --tags doc,man -C $srcdir/MangoHud/build_64 --destdir "$pkgdir"

  mv "${pkgdir}"/usr/share/vulkan/implicit_layer.d/MangoHud.json "${pkgdir}"/usr/share/vulkan/implicit_layer.d/MangoHud.x86_64.json

  sed -i 's|VK_LAYER_MANGOHUD_overlay|VK_LAYER_MANGOHUD_overlay_64|g' "${pkgdir}"/usr/share/vulkan/implicit_layer.d/MangoHud.x86_64.json

  sed -i 's|$LIB|lib|g' "${pkgdir}"/usr/share/vulkan/implicit_layer.d/MangoHud.x86_64.json

  install -Dm644 $srcdir/MangoHud/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

package_mangoapp(){
  pkgdesc='A transparent background OpenGL application with a built-in MangoHud designed to be run inside a gamescope instance'
  depends=(glew glfw-x11 mangohud)
  optdepends=('gamescope')

  meson install --tags "$pkgname" -C $srcdir/MangoHud/build_64 --destdir "$pkgdir"

  mv "${pkgdir}"/usr/share/vulkan/implicit_layer.d/libMangoApp.json "${pkgdir}"/usr/share/vulkan/implicit_layer.d/libMangoApp.x86_64.json

  sed -i 's|VK_LAYER_MANGOAPP_overlay|VK_LAYER_MANGOAPP_overlay_64|g' "${pkgdir}"/usr/share/vulkan/implicit_layer.d/libMangoApp.x86_64.json

  sed -i 's|$LIB|lib|g' "${pkgdir}"/usr/share/vulkan/implicit_layer.d/libMangoApp.x86_64.json

  install -Dm644 $srcdir/MangoHud/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

sha256sums=('SKIP'
            'SKIP'
            'e110beffda505e6954feb7b13541d35a7c12a176b9723290c853684713df6a67'
            '57dd9a868539dbd1b1d3702eeef47a1b6b16cd7cd7adc07352f204ed2d09a11d'
            'ca5cae8d6cac15dae0ec63b21d6ad3530070650f68076f3a4a862ca293a858bb'
            'e7bffdea35f49decededa6195856d5e04781eb3d322437ee970446d3bf4b9c39')
